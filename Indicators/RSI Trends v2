//@version=5
indicator(title="RSI Trends v2", format=format.price, precision=2, timeframe="", timeframe_gaps=true)

ma(source, length, type) =>
    switch type
        "SMA" => ta.sma(source, length)
        "EMA" => ta.ema(source, length)
        "SMMA (RMA)" => ta.rma(source, length)
        "WMA" => ta.wma(source, length)
        "VWMA" => ta.vwma(source, length)

// Input Settings
rsiLengthInput = input.int(37, minval=1, title="RSI Length", group="RSI Settings")
rsiSourceInput = input.source(close, "Source", group="RSI Settings")
maTypeInput = input.string("EMA", title="MA Type", options=["SMA", "EMA", "SMMA (RMA)", "WMA", "VWMA"], group="MA Settings")
maLengthInput = input.int(38, title="MA Length", group="MA Settings")
rsiLookback = input.int(15, title="Range Look Back Period")
rsiUpperBandValue = input.int(54, title="Upper Band")
rsiLowerBandValue = input.int(46, title="Lower Band")

// RSI and MA Calculation
up = ta.rma(math.max(ta.change(rsiSourceInput), 0), rsiLengthInput)
down = ta.rma(-math.min(ta.change(rsiSourceInput), 0), rsiLengthInput)
rsi = down == 0 ? 100 : up == 0 ? 0 : 100 - (100 / (1 + up / down))
rsiMA = ma(rsi, maLengthInput, maTypeInput)

// HMA Calculation
hma(source, length) =>
    halfLength = math.round(length / 2)
    sqrtLength = math.round(math.sqrt(length))
    wma1 = ta.wma(source, length)
    wma2 = ta.wma(source, halfLength)
    ta.wma(2 * wma2 - wma1, sqrtLength)

hmaLength = input.int(21, title="HMA Length", group="HMA Settings")
hmaValue = hma(rsiMA, hmaLength)

// HMA Trend Detection
hmaColor = hmaValue > hmaValue[1] ? color.green : color.red

// Plot RSI and MA
plot(rsi, "RSI", color=#7E57C2)
rsiMAColor = color.green
numberOfTimesInRange = 0

for i = 0 to rsiLookback
    if (rsiMA[i] < rsiUpperBandValue and rsiMA[i] > rsiLowerBandValue)
        numberOfTimesInRange := numberOfTimesInRange + 1

if (numberOfTimesInRange >= rsiLookback)
    rsiMAColor := color.purple
else if (rsiMA > rsiUpperBandValue)
    rsiMAColor := color.green
else if (rsiMA < rsiLowerBandValue)
    rsiMAColor := color.red
else
    rsiMAColor := color.gray

plot(rsiMA, "RSI-based MA", color=rsiMAColor)

// Plot HMA on top of MA line
plot(hmaValue, "HMA", color=hmaColor, linewidth=2)

// Bands and Fill
rsiUpperBand = hline(rsiUpperBandValue, "RSI Upper Band", color=#787B86)
midline = hline(50, "RSI Middle Band", color=color.new(#787B86, 50))
rsiLowerBand = hline(rsiLowerBandValue, "RSI Lower Band", color=#787B86)

fill(rsiUpperBand, rsiLowerBand, color=color.rgb(126, 87, 194, 90), title="RSI Background Fill")
